; From RFC 5234 "Augmented BNF for Syntax Specifications: ABNF"

ALPHA       = %x41-5A / %x61-7A   ; A-Z / a-z
DIGIT       = %x30-39             ; 0-9

OCTET          =  %x00-FF
                  ; 8 bits of data

; From RFC 3629
UTF8-2      = %xC2-DF UTF8-tail
UTF8-3      = %xE0 %xA0-BF UTF8-tail / %xE1-EC 2( UTF8-tail ) /
              %xED %x80-9F UTF8-tail / %xEE-EF 2( UTF8-tail )
UTF8-4      = %xF0 %x90-BF 2( UTF8-tail ) / %xF1-F3 3( UTF8-tail ) /
              %xF4 %x80-8F 2( UTF8-tail )
UTF8-tail   = %x80-BF

; From RFC 5802
attr-val        = 1*ALPHA "=" value
                     ;; Generic syntax of any attribute sent
                     ;; by server or client

value           = 1*value-char

value-safe-char = %x01-2B / %x2D-3C / %x3E-7F /
                  UTF8-2 / UTF8-3 / UTF8-4
                  ;; UTF8-char except NUL, "=", and ",".

value-char      = value-safe-char / "="

printable       = %x21-2B / %x2D-7E
                  ;; Printable ASCII except ",".
                  ;; Note that any "printable" is also
                  ;; a valid "value".

base64-char     = ALPHA / DIGIT / "/" / "+"

base64-4        = 4base64-char

base64-3        = 3base64-char "="

base64-2        = 2base64-char "=="

base64          = *base64-4 [base64-3 / base64-2]

posit-number    = %x31-39 *DIGIT
                  ;; A positive number.

saslname        = 1*(value-safe-char / "=2C" / "=3D")
                  ;; Conforms to <value>.

authzid         = "a=" saslname
                  ;; Protocol specific.

cb-name         = 1*(ALPHA / DIGIT / "." / "-")
                  ;; See RFC 5056, Section 7.
                  ;; E.g., "tls-server-end-point" or
                  ;; "tls-unique".

gs2-cbind-flag  = ("p=" cb-name) / "n" / "y"
                  ;; "n" -> client doesn't support channel binding.
                  ;; "y" -> client does support channel binding
                  ;;        but thinks the server does not.
                  ;; "p" -> client requires channel binding.
                  ;; The selected channel binding follows "p=".

gs2-header      = gs2-cbind-flag "," [ authzid ] ","
                  ;; GS2 header for SCRAM
                  ;; (the actual GS2 header includes an optional
                  ;; flag to indicate that the GSS mechanism is not
                  ;; "standard", but since SCRAM is "standard", we
                  ;; don't include that flag).

username        = "n=" saslname
                  ;; Usernames are prepared using SASLprep.

reserved-mext   = "m=" 1*(value-char)
                  ;; Reserved for signaling mandatory extensions.
                  ;; The exact syntax will be defined in
                  ;; the future.

channel-binding = "c=" base64
                  ;; base64 encoding of cbind-input.

proof           = "p=" base64

nonce           = "r=" c-nonce [s-nonce]
                  ;; Second part provided by server.

c-nonce         = *printable

s-nonce         = *printable

salt            = "s=" base64

verifier        = "v=" base64
                  ;; base-64 encoded ServerSignature.

iteration-count = "i=" posit-number
                  ;; A positive number.

client-first-message-bare =
                  [reserved-mext ","]
                  username "," nonce ["," extensions]

client-first-message =
                  gs2-header client-first-message-bare

server-first-message =
                  [reserved-mext ","] nonce "," salt ","
                  iteration-count ["," extensions]

client-final-message-without-proof =
                  channel-binding "," nonce ["," extensions]

client-final-message =
                  client-final-message-without-proof "," proof

server-error = "e=" server-error-value

server-error-value = "invalid-encoding" /
                  "extensions-not-supported" /  ; unrecognized 'm' value
                  "invalid-proof" /
                  "channel-bindings-dont-match" /
                  "server-does-support-channel-binding" /
                  ; server does not support channel binding
                  "channel-binding-not-supported" /
                  "unsupported-channel-binding-type" /
                  "unknown-user" /
                  "invalid-username-encoding" /
                  ; invalid username encoding (invalid UTF-8 or
                  ; SASLprep failed)
                  "no-resources" /
                  "other-error" /
                  server-error-value-ext
                  ; Unrecognized errors should be treated as "other-error".
                  ; In order to prevent information disclosure, the server
                  ; may substitute the real reason with "other-error".

server-error-value-ext = value
                  ; Additional error reasons added by extensions
                  ; to this document.

server-final-message = (server-error / verifier) ["," extensions]

extensions = attr-val *("," attr-val)
                  ;; All extensions are optional,
                  ;; i.e., unrecognized attributes
                  ;; not defined in this document
                  ;; MUST be ignored.

cbind-data    = 1*OCTET

cbind-input   = gs2-header [ cbind-data ]
                  ;; cbind-data MUST be present for
                  ;; gs2-cbind-flag of "p" and MUST be absent
                  ;; for "y" or "n".

